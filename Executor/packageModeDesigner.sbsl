конст ПутиV8 = {"eq": "c:\\Program Files\\1Cv8\\", "ne": "c:\\Program Files (x86)\\1Cv8\\"}
конст РабочийКаталогПоУмолчанию = "c:\\temp\\executor\\"
конст РежимЗапуска = {0 : "DESIGNER", 1 : "ENTERPRISE"}

метод Скрипт(ПутьКФайлуНастроек : Строка)
    пер ФайлНастроек = новый Файл(ПутьКФайлуНастроек)
    пер Настройки = СериализацияJson.ПрочитатьОбъект(ФайлНастроек.ОткрытьПотокЧтения())

    пер ИмяБазы = Настройки["base"]
    Консоль.Записать("Имя базы: %ИмяБазы")
    пер Сервер = Настройки["server"]
    Консоль.Записать("Адрес сервера: %Сервер")
    пер Версия = Настройки["platformVersion"]
    Консоль.Записать("Версия платформы: %Версия")
    пер ИмяПользователя = Настройки["baseUsername"]
    Консоль.Записать("Пользователь  базы: %ИмяПользователя")
    пер Пароль = Настройки["basePassword"]
    Консоль.Записать("Пароль: %Пароль")
    пер РабочийКаталог = Настройки["workDirectory"]
    Консоль.Записать("Рабочий каталог: %РабочийКаталог")
    пер SQLServer = Настройки["SQLServer"]
    Консоль.Записать("SQLServer: %SQLServer")
    пер SQLUser = Настройки["SQLUser"]
    Консоль.Записать("SQLUser: %SQLUser")
    пер SQLPassword = Настройки["SQLPassword"]
    Консоль.Записать("SQLPassword: %SQLPassword")
    пер ПутьКСкриптуСозданияКопииSQL = Настройки["scriptPathBackup"]
    Консоль.Записать("Путь к срипту создания копии SQL: %ПутьКСкриптуСозданияКопииSQL")
    пер ИмяБазыДляВосстановления = Настройки["baseRestore"]
    Консоль.Записать("База для восстановления копии SQL: %ИмяБазыДляВосстановления")
    пер ПутьКСкриптуВосстановленияКопииSQL = Настройки["scriptPathRestore"]
    Консоль.Записать("Путь к срипту создания копии SQL: %ПутьКСкриптуВосстановленияКопииSQL")
    пер КаталогКопииSQL = Настройки["backUpCatalogName"]
    Консоль.Записать("Каталог для создания копии SQL: %КаталогКопииSQL")
    пер ПутьКОбработке = Настройки["executeDataProcessor"]
    Консоль.Записать("Путь к обработке для исполнения: %ПутьКОбработке")
    пер МассивПараметров = Настройки["executeDataProcessorParameters"]
    пер СтрокаПараметров = ПолучитьСтрокуПараметров(МассивПараметров)
    пер ПутьКХранилищу = Настройки["storage"]
    Консоль.Записать("Хранилище: %ПутьКХранилищу")
    пер ИмяПользователяХранилища = Настройки["storageUser"]
    Консоль.Записать("Пользователь хранилища: %ИмяПользователяХранилища")
    пер ПарольПользователяХранилища = Настройки["storagePassword"]


    пер ТекущаяДата = ДатаВремя.Сейчас().Форматировать("ддММггггЧЧммсс")
    пер ИмяФайлаКопииSQL = "%КаталогКопииSQL%ИмяБазы%ТекущаяДата.bak"
    Консоль.Записать("Файл копии SQL: %ИмяФайлаКопииSQL")
    
    СохранитьКонфигурациюВФайл(ИмяБазыДляВосстановления, Сервер, ИмяПользователя, Пароль, Версия)
    ВыгрузитьКопиюСредствамиSQL(SQLServer, SQLUser, SQLPassword, ИмяБазы, ИмяФайлаКопииSQL, ПутьКСкриптуСозданияКопииSQL)
    ВоосстановитьКопиюСредствамиSQL(SQLServer, SQLUser, SQLPassword, ИмяБазыДляВосстановления, ИмяФайлаКопииSQL, ПутьКСкриптуВосстановленияКопииSQL)
    ОтключитьКонфигурациюОтХранилища(ИмяБазыДляВосстановления, Сервер, ИмяПользователя, Пароль, Версия)
    ПодключитьКонфигурациюКХранилищу(ИмяБазыДляВосстановления, Сервер, ИмяПользователя, Пароль, ПутьКХранилищу, ИмяПользователяХранилища, ПарольПользователяХранилища, Версия)
    ОбновитьКонфигурациюИзХранилища(ИмяБазыДляВосстановления, Сервер, ИмяПользователя, Пароль, ПутьКХранилищу, ИмяПользователяХранилища, ПарольПользователяХранилища, Версия)
    ЗапуститьОбработкуСПараметрами(ИмяБазыДляВосстановления, Сервер, ИмяПользователя, Пароль, ПутьКОбработке, СтрокаПараметров, Версия)
;

метод СохранитьКонфигурациюВФайл(ИмяБазы : Строка, Сервер : Строка, ИмяПользователя : Строка, Пароль : Строка, Версия : Строка, 
    РабочийКаталог : Строка = "")
    пер ПутьКБазе = ПолучитьПутьКБазе(ИмяБазы, Сервер)
    пер МассивАргументов = ПолучитьОбщийМассивПараметров(ПутьКБазе, ИмяПользователя, Пароль)
    
    если РабочийКаталог.Пусто()
        РабочийКаталог = РабочийКаталогПоУмолчанию        
    ;
    пер ТекущаяДата = ДатаВремя.Сейчас().Форматировать("ддММггггЧЧммсс")
    пер ИмяФайла = "%ИмяБазы%ТекущаяДата.cf"
    // МассивАргументов.Добавить("/CreateDistributionFiles")
    // МассивАргументов.Добавить("-cffile %РабочийКаталог%ИмяФайла")
    МассивАргументов.Добавить("/DumpCfg %РабочийКаталог%ИмяФайла")
    МассивАргументов.Добавить("/Out %РабочийКаталог\\logcffile.txt")

    пер Процесс = ВыполнитьКомандуПакетногоРежима(Версия, МассивАргументов)
    пока Процесс.ПолучитьКодВозврата() == Неопределено
        Консоль.Записать(Процесс.ПолучитьПотокВывода().ПрочитатьКакТекст())
    ;
    Консоль.Записать("Выгрузка завершена " + ДатаВремя.Сейчас().Форматировать("ЧЧ:мм:сс") + " Путь к файлу: %РабочийКаталог%ИмяФайла")
;

метод ПодключитьКонфигурациюКХранилищу(ИмяБазы : Строка, Сервер : Строка, ИмяПользователя : Строка, Пароль : Строка, ПутьКХранилищу : Строка, ИмяПользователяХранилища : Строка, ПарольПользователяХранилища : Строка, Версия : Строка, ПутьКЛогамКонфигуратора : Строка = "")
    пер ПутьКБазе = ПолучитьПутьКБазе(ИмяБазы, Сервер)
    пер МассивАргументов = ПолучитьОбщийМассивПараметров(ПутьКБазе, ИмяПользователя, Пароль)       
    пер МассивАргументовХранилища = ПолучитьМассивДляРаботыСХранилищем(ПутьКХранилищу, ИмяПользователяХранилища, ПарольПользователяХранилища)

    МассивАргументов.ДобавитьВсе(МассивАргументовХранилища)
    
    МассивАргументов.Добавить("/ConfigurationRepositoryBindCfg")
    МассивАргументов.Добавить("-forceBindAlreadyBindedUser")
    МассивАргументов.Добавить("-forceReplaceCfg")
    пер Процесс = ВыполнитьКомандуПакетногоРежима(Версия, МассивАргументов)
    пер Результат = 1
    пока Процесс.ПолучитьКодВозврата() == Неопределено
        Консоль.Записать(Процесс.ПолучитьПотокВывода().ПрочитатьКакТекст())
    ;

    Результат = Процесс.ПолучитьКодВозврата()
    если Результат == 0 
        Консоль.Записать("База обновлена из хранилща " + ДатаВремя.Сейчас().Форматировать("ЧЧ:мм:сс"))
    иначе
        Консоль.Записать("Ошибка Обновления базы из хранилища")
    ;
;

метод ОтключитьКонфигурациюОтХранилища(ИмяБазы : Строка, Сервер : Строка, ИмяПользователя : Строка, Пароль : Строка, Версия : Строка)
    пер ПутьКБазе = ПолучитьПутьКБазе(ИмяБазы, Сервер)
    пер МассивАргументов = ПолучитьОбщийМассивПараметров(ПутьКБазе, ИмяПользователя, Пароль)       
    
    МассивАргументов.Добавить("/ConfigurationRepositoryUnbindCfg")
    МассивАргументов.Добавить("-force")
    пер Процесс = ВыполнитьКомандуПакетногоРежима(Версия, МассивАргументов)
    пер Результат = 1
    пока Процесс.ПолучитьКодВозврата() == Неопределено
        Консоль.Записать(Процесс.ПолучитьПотокВывода().ПрочитатьКакТекст())
    ;

    Результат = Процесс.ПолучитьКодВозврата()
    если Результат == 0 
        Консоль.Записать("База отключена от хранилища " + ДатаВремя.Сейчас().Форматировать("ЧЧ:мм:сс"))
    иначе
        Консоль.Записать("Ошибка отключения базы от хранилища")
    ;
;

метод ОбновитьКонфигурациюИзХранилища(ИмяБазы : Строка, Сервер : Строка, ИмяПользователя : Строка, Пароль : Строка, ПутьКХранилищу : Строка, ИмяПользователяХранилища : Строка, ПарольПользователяХранилища : Строка, Версия : Строка, ПутьКЛогамКонфигуратора : Строка = "")
    Консоль.Записать("")
    Консоль.Записать("Обновление конфигурации из хранилища")

    пер ПутьКБазе = ПолучитьПутьКБазе(ИмяБазы, Сервер)
    пер МассивАргументов = ПолучитьОбщийМассивПараметров(ПутьКБазе, ИмяПользователя, Пароль)       
    пер МассивАргументовХранилища = ПолучитьМассивДляРаботыСХранилищем(ПутьКХранилищу, ИмяПользователяХранилища, ПарольПользователяХранилища)

    МассивАргументов.ДобавитьВсе(МассивАргументовХранилища)
    
    МассивАргументов.Добавить("/ConfigurationRepositoryUpdateCfg")
    МассивАргументов.Добавить("-force")
    МассивАргументов.Добавить("/UpdateDBCfg")
    пер Процесс = ВыполнитьКомандуПакетногоРежима(Версия, МассивАргументов)
    пер Результат = 1
    Процесс.ОжидатьЗавершения()

    Результат = Процесс.ПолучитьКодВозврата()
    если Результат == 0 
        Консоль.Записать("База обновлена из хранилща " + ДатаВремя.Сейчас().Форматировать("ЧЧ:мм:сс"))
    иначе
        Консоль.Записать("Ошибка Обновления базы из хранилища")
    ;
;

метод ВыгрузитьКопиюСредствамиSQL(SQLServer : Строка, SQLUser: Строка, SQLPassword : Строка, ИмяБазы : Строка, ИмяФайлаКопиSQL : Строка, ПутьКСрипту : Строка, РабочийКаталог : Строка = "")
    если РабочийКаталог.Пусто()
        РабочийКаталог = РабочийКаталогПоУмолчанию        
    ;

    пер МассивАргументов = новый Массив()
    МассивАргументов.Добавить("-S%SQLServer")
    МассивАргументов.Добавить("-U%SQLUser")
    МассивАргументов.Добавить("-P%SQLPassword")
    МассивАргументов.Добавить("-i%ПутьКСрипту")
    МассивАргументов.Добавить("-b")
    МассивАргументов.Добавить("-v")
    МассивАргументов.Добавить("backupdb=%ИмяБазы")
    МассивАргументов.Добавить("-v")
    МассивАргументов.Добавить("bakfile=%ИмяФайлаКопиSQL")

    пер Процесс = новый ПроцессОс("sqlcmd", МассивАргументов)
    Процесс.Запустить(СредаИсполнения.ПолучитьПеременную("temp"))
    пер Результат = 1
    пока Процесс.ПолучитьКодВозврата() == Неопределено
        Консоль.Записать(Процесс.ПолучитьПотокВывода().ПрочитатьКакТекст())
    ;

    Результат = Процесс.ПолучитьКодВозврата()
    если Результат == 0 
        Консоль.Записать("Выгрузка завершена " + ДатаВремя.Сейчас().Форматировать("ЧЧ:мм:сс") + " Путь к файлу: %РабочийКаталог%ИмяФайлаКопиSQL")
    иначе
        Консоль.Записать("Ошибка создания копии")
    ;
;

метод ВоосстановитьКопиюСредствамиSQL(SQLServer : Строка, SQLUser: Строка, SQLPassword : Строка, ИмяБазы : Строка, ИмяФайла : Строка, ПутьКСрипту : Строка, РабочийКаталог : Строка = "")
    если РабочийКаталог.Пусто()
        РабочийКаталог = РабочийКаталогПоУмолчанию        
    ;
    
    пер МассивАргументов = новый Массив()
    МассивАргументов.Добавить("-S%SQLServer")
    МассивАргументов.Добавить("-U%SQLUser")
    МассивАргументов.Добавить("-P%SQLPassword")
    МассивАргументов.Добавить("-i%ПутьКСрипту")
    МассивАргументов.Добавить("-b")
    МассивАргументов.Добавить("-v")
    МассивАргументов.Добавить("restoreddb=%ИмяБазы")
    МассивАргументов.Добавить("-v")
    МассивАргументов.Добавить("bakfile=%ИмяФайла")

    пер Процесс = новый ПроцессОс("sqlcmd", МассивАргументов)
    Процесс.Запустить(СредаИсполнения.ПолучитьПеременную("temp"))
    пока Процесс.ПолучитьКодВозврата() == Неопределено
        Консоль.Записать(Процесс.ПолучитьПотокВывода().ПрочитатьКакТекст())
    ;
    Консоль.Записать("Выгрузка завершена " + ДатаВремя.Сейчас().Форматировать("ЧЧ:мм:сс") + " Путь к файлу: %РабочийКаталог%ИмяФайла")
;

метод ЗапуститьОбработкуСПараметрами(ИмяБазы : Строка, Сервер : Строка,  ИмяПользователя : Строка, Пароль : Строка, ПутьКОбработке : Строка, Параметры : Строка, Версия : Строка)
    пер ПутьКБазе = ПолучитьПутьКБазе(ИмяБазы, Сервер)
    пер МассивАргументов = ПолучитьОбщийМассивПараметров(ПутьКБазе, ИмяПользователя, Пароль, 1)

    МассивАргументов.Добавить("/Execute")
    МассивАргументов.Добавить(ПутьКОбработке)
    МассивАргументов.Добавить(Параметры)

    пер Процесс = ВыполнитьКомандуПакетногоРежима(Версия, МассивАргументов)
    пер Результат = 1
    пока Процесс.ПолучитьКодВозврата() == Неопределено
        Консоль.Записать(Процесс.ПолучитьПотокВывода().ПрочитатьКакТекст())
    ;
    Результат = Процесс.ПолучитьКодВозврата()
    если Результат == 0 
        Консоль.Записать("Обработка выполнена")
    иначе
        Консоль.Записать(Результат)
        Консоль.Записать("Ошибка")
    ;
;

// TODO Перенести в общую библиотеку, когда это будет доступно
метод ВыполнитьКомандуПакетногоРежима(Версия: Строка, МассивАргументов : Массив) : ПроцессОс
    пер АрхитектураОС: Число = ТекущаяАрхитектура()
    пер ПутьV8: Строка

    если Версия.ЧислоВхождений(".") != 3
        Консоль.ЗаписатьОшибку("Номер версии указан некорректно")
        выбросить новый ИсключениеНедопустимоеСостояние("Номер версии указан некорректно")
    ;
    выбор

    когда АрхитектураОС == 32
        ПутьV8 = ПутиV8["eq"]
    когда АрхитектураОС == 64
        ПутьV8 = ПутиV8["ne"]
    иначе
        выбросить новый ИсключениеНедопустимоеСостояние("64-разрядная платформа V8 не может быть установлена на 32-разрядной ОС")
    ;

    пер Образ = новый Файл("%ПутьV8\\%Версия\\bin\\1cv8.exe")
    если не Образ.Существует()
        выбросить новый ИсключениеНедопустимоеСостояние("Экземляр платформы 1с не обнаружен в каталоге: " + Образ.Каталог)
    ;

    пер Процесс = новый ПроцессОс(Образ.Путь, МассивАргументов)
    Процесс.Запустить(СредаИсполнения.ПолучитьПеременную("temp"))
    если не Процесс.Живой()
        Консоль.ЗаписатьОшибку("Что-то пошло не так. Процесс 1с - не живой :(")
    иначе
        Консоль.Записать("Начало выполнения операции: " + ДатаВремя.Сейчас().Форматировать("ЧЧ:мм:сс"))
    ;
    возврат Процесс
;

// TODO Перенести в общую библиотеку, когда это будет доступно
метод ПолучитьПутьКБазе(ИмяБазы : Строка, Сервер : Строка) : Строка
    пер путьКБазе : Строка
    если Сервер == "" 
        путьКБазе = "/F%ИмяБазы"
    иначе
        путьКБазе = "/s%Сервер\\%ИмяБазы"
    ;
    возврат путьКБазе   
;

// TODO Перенести в общую библиотеку, когда это будет доступно
метод ТекущаяАрхитектура(): Число
    пер Архитектура = СредаИсполнения.ПолучитьПеременную("PROCESSOR_ARCHITECTURE")
    пер Результат = 0
    выбор Архитектура.ВВерхнийРегистр()
    когда == "AMD64"
        Результат = 64
    когда == "X86"
        Результат = 32
    иначе
        Результат = 32
    ;
    возврат Результат
;

// TODO Перенести в общую библиотеку, когда это будет доступно
метод ПолучитьОбщийМассивПараметров(ПутьКБазе : Строка, ИмяПользователя : Строка, Пароль : Строка, Режим : Число = 0, ПутьКЛогамКонфигуратора : Строка = "") : Массив
    пер МассивАргументов = новый Массив() 
    если ПутьКЛогамКонфигуратора.Пусто()
        ПутьКЛогамКонфигуратора = РабочийКаталогПоУмолчанию + "cfglog.txt"
    ;
        
    МассивАргументов.Добавить(РежимЗапуска[Режим])
    МассивАргументов.Добавить("/WA-")
    МассивАргументов.Добавить("/DISABLESTARTUPDIALOGS")
    МассивАргументов.Добавить(ПутьКБазе)
    МассивАргументов.Добавить("/N%ИмяПользователя")
    МассивАргументов.Добавить("/P%Пароль")
    МассивАргументов.Добавить("/Out%ПутьКЛогамКонфигуратора")

    возврат МассивАргументов
;

метод ПолучитьСтрокуПараметров(МассивПараметров : Массив): Строка
    пер СтрокаПараметров = "/C" + "\""

    для элемент из МассивПараметров
        Консоль.Записать(элемент) 
        СтрокаПараметров = СтрокаПараметров + элемент + ";"
    ;
    СтрокаПараметров = СтрокаПараметров.ПодстрокаСНачала(СтрокаПараметров.Длина() - 1) + "\""
    Консоль.Записать(СтрокаПараметров)
    возврат СтрокаПараметров
;

метод ПолучитьМассивДляРаботыСХранилищем(ПутьКХранилищу: Строка, ИмяПользователяХранилища : Строка, ПарольПользователяХранилища : Строка): Массив
    пер МассивПараметровХранилища = новый Массив()
    МассивПараметровХранилища.Добавить("/ConfigurationRepositoryF%ПутьКХранилищу")
    МассивПараметровХранилища.Добавить("/ConfigurationRepositoryN%ИмяПользователяХранилища")
    МассивПараметровХранилища.Добавить("/ConfigurationRepositoryP%ПарольПользователяХранилища")

    возврат МассивПараметровХранилища
;